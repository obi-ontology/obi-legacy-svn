# Curation status should be set and with meta-data imcomplete at least for release
==================================================================================


# Check curation status of all OBI terms (ignore deprecated terms)
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?class ?label ?status
WHERE {
	?class a owl:Class.
	?class rdfs:label ?label.
	optional {
		?class obo:IAO_0000114 ?status.
	}
	FILTER (regex(str(?class),"OBI_") && !(regex(str(?label),"^obsolete"))).
}
ORDER BY ?status ?label
--------------------------------


# Only work on Twinkle SPARQL tools, all queries with OPTIONAL do not work well on Protege 3.x SPARQL tool some time (worked fine before)
# List all OBI classes which do not have 'has curation status' property
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?class ?label ?status
WHERE {
	?class a owl:Class.
	?class rdfs:label ?label.
	optional {
		?class obo:IAO_0000114 ?status.
	}
	FILTER (regex(str(?class),"OBI_") && !(regex(str(?label),"^obsolete")) && !bound(?status)).
}
ORDER BY ?label
--------------------------------


# Only work on Twinkle SPARQL tools, all queries with OPTIONAL do not work on Protege 3.x SPARQL tool (worked well before)
# List all OBI terms which do not have 'has curation status' property and has 'definition editor' property
# Note: will miss the terms which don't have 'definition editor' property
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?class ?label ?editor ?status
WHERE {
	?class rdfs:label ?label.
	?class obo:IAO_0000117 ?editor .
	optional {
		?class obo:IAO_0000114 ?status.
	}
	FILTER (regex(str(?class),"OBI_") && !(regex(str(?label),"^obsolete")) && !bound(?status)).
}
ORDER BY ?class
--------------------------------


# List all OBI terms which 'has curation status' property is uncurated
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?term ?label ?status
WHERE {
	?term rdfs:label ?label.
	?term obo:IAO_0000114 ?status.
	FILTER (regex(str(?term),"OBI_") && !(regex(str(?label),"^obsolete")) && ?status = obo:IAO_0000124).
}
--------------------------------


# List all OBI terms which 'has curation status' properties do not have value
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?label ?status
WHERE {
	?term rdfs:label ?label.
	?term obo:IAO_0000114 ?status.
	FILTER (regex(str(?term),"OBI_") && !(regex(str(?label),"^obsolete")) && ?status = '').
}
==================================================================================



# The label and editor preferred term of all OBI terms should be same
# Find terms with inconsistent label and editor preferred term
=======================================================================

PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?term ?label ?preferred ?editor
WHERE {
?term rdfs:label ?label .
?term obo:IAO_0000111 ?preferred .
?term rdfs:subClassOf ?parent .
optional {
	?term obo:IAO_0000117 ?editor .
}
FILTER ((!((str(?label) = str(?preferred)))) &&
(!(regex(str(?label),"(?s)^obsolete.*"))) && (!((str(?preferred) =""))) && (!((?parent =<http://www.geneontology.org/formats/oboInOwl#ObsoleteClass> ))))}
==================================================================================



# Should not have duplicated label
# list all OBI terms and manullly check whether there are duplicate terms
# should do it automatically using script
=======================================================================

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT distinct ?term ?label
WHERE {
	?term rdfs:label ?label.
	FILTER (regex(str(?term),"OBI_") && !(regex(str(?label),"^obsolete"))).
}
ORDER BY ?label
==================================================================================



# Check any obsoleted classes not the subclasses of 'ObsoleteClass'
====================================================================

# Check all classes has obsolete reason are the subclasses of obsolete
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?term ?label ?super ?reason
WHERE {
	?term rdfs:label ?label .
	?term rdfs:subClassOf ?super .
	?term obo:IAO_0000231 ?reason
}
ORDER BY ?super
--------------------------------

# Check all subclasses of ObsoleteClass has obsolete reason
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obsolete: <http://www.geneontology.org/formats/oboInOwl#>

SELECT DISTINCT ?term ?label ?reason
WHERE {
	?term rdfs:label ?label .
	?term rdfs:subClassOf ?super .
	optional {
		?term obo:IAO_0000231 ?reason.
	}
	FILTER (?super = obsolete:ObsoleteClass && !bound(?reason)).
}
ORDER BY ?super
==================================================================================

